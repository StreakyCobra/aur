# Maintainer: Fabien Dubosson <fabien.dubosson@gmail.com>
pkgname=vstax
_year=2013
pkgver=${_year}.1.0.3
pkgrel=1
pkgdesc="Software to fill the tax forms of the canton of Valais"
arch=('i686' 'x86_64')
url="http://www.vs.ch/vstax"
license=('unknown')
depends=('java-runtime' 'shared-mime-info' 'desktop-file-utils')
install="vstax.install"
changelog="ChangeLog"
[ "${CARCH}" = "i686" ] && _CARCH='i386'
[ "${CARCH}" = "x86_64" ] && _CARCH='64'
source=("http://www.vs.ch/home2/${pkgname}/linux/${pkgname}${_year:2:2}/${pkgname}${_year}_${_CARCH}.deb"
        "archlinux.patch")
md5sums=(''
         '9b1e26ef419b013945c6ea3258b4a064')
[ "${CARCH}" = "i686" ] && md5sums[0]='9949e6b72e0e9b0b4b5938592db6829a'
[ "${CARCH}" = "x86_64" ] && md5sums[0]='9631d865e423ee5c2496478ba76d4df0'

prepare() {
    cd "${srcdir}"

    # Extract the data contained in the `deb` file
    tar -xzf data.tar.gz

    # Patch for archlinux
    patch -p2 < archlinux.patch
}

package() {
    cd "${srcdir}/usr/share"

    # Store application name in a variable
    _appname="${pkgname}${_year}"

    # Copy the application descriptor
    install -D "applications/${_appname}.desktop" "${pkgdir}/usr/share/applications/${_appname}.desktop"

    # Copy the application changelog
    install -D "doc/${_appname}/changelog.Debian.gz" "${pkgdir}/usr/share/doc/${_appname}/changelog.Debian.gz"

    # Copy the mime descriptor
    install -D "mime/packages/${_appname}.xml" "${pkgdir}/usr/share/mime/packages/${_appname}.xml"

    # Copy the application itself
    install -d "${pkgdir}/usr/share/java/"
    cp -R "${_appname}" "${pkgdir}/usr/share/java/${_appname}"

    # Link executable to /usr/bin
    install -d "${pkgdir}/usr/bin/"
    mv "${pkgdir}/usr/share/java/${_appname}/${_appname}.sh" "${pkgdir}/usr/bin/${_appname}"

    # Remove the included java environment
    rm -Rf "${pkgdir}/usr/share/java/${_appname}/jre"
}
