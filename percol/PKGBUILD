# Maintainer: Fabien Dubosson <fabien.dubosson@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=percol
pkgver=0.0.8
pkgrel=1
pkgdesc="Filter the output of a command interactively, inspired by anything.el for Emacs"
arch=('any')
depends=('python2')
makedepends=('python2-setuptools')
url="https://github.com/mooz/percol"
license=('MIT')
options=(!emptydirs)
source=(https://github.com/mooz/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('c0f67ddf84e944d5446590b2a63a54dcc20585e8177b170b2641cc042ef52035')
provides=('percol' 'python2-percol')
conflicts=('python2-percol' 'percol-git')
install=percol.install

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg 'Installing...'
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
