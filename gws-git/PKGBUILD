# Maintainer: Fabien Dubosson <fabien.dubosson@gmail.com>
pkgname=gws-git
_pkgname=gws
pkgver=0.1.1.r13.gd42e36f
pkgrel=1
pkgdesc="An helper to manage workspaces which contains git repositories"
arch=('any')
url="https://github.com/StreakyCobra/gws"
license=('MIT')
depends=('bash>4.0' 'git')
changelog="ChangeLog"
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("gws::git+https://github.com/StreakyCobra/gws.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd "${srcdir}/${_pkgname}"

  install -D -m 755 "gws" "${pkgdir}/usr/bin/gws"
  install -D -m 755 "completions/zsh" "${pkgdir}/usr/share/zsh/site-functions/_gws"
  install -D -m 755 "completions/bash" "${pkgdir}/usr/share/bash-completion/completions/gws"
}
