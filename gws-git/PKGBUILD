# Maintainer: Fabien Dubosson <fabien.dubosson@gmail.com>

pkgname="gws-git"
_pkgname="gws"
pkgver="0.1.3.r2.g7ed3fb5"
pkgrel="1"
pkgdesc="An helper to manage workspaces which contains git repositories"
url="https://github.com/StreakyCobra/gws"
license=('MIT')
arch=('any')
depends=('bash>4.0' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
changelog="ChangeLog"
source=("${_pkgname}::git+https://github.com/StreakyCobra/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}/"

  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd "${srcdir}/${_pkgname}/"

  install -D -m 755 'gws' "${pkgdir}/usr/bin/${_pkgname}"
  install -D -m 755 'completions/zsh' "${pkgdir}/usr/share/zsh/site-functions/_${_pkgname}"
  install -D -m 755 'completions/bash' "${pkgdir}/usr/share/bash-completion/completions/${_pkgname}"
}

# vim:set ts=4 sw=4 et:
