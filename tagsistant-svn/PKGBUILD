# Maintainer: Fabien Dubosson <fabien dot dubosson at gmail dot com>
pkgname=tagsistant-svn
_pkgname=tagsistant
pkgver=445
pkgrel=2
pkgdesc="Semantic File System for Linux kernels"
arch=('i686' 'x86_64')
url="https://www.tagsistant.net/"
license=('GPL')
depends=('fuse' 'libdbi' 'libdbi-drivers' 'sqlite2' 'sqlite' 'libmariadbclient' 'postgresql-libs' 'glib2' 'libextractor')
makedepends=('subversion')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::svn+http://svn.gna.org/svn/tagfs/trunk")
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  svnversion | tr -d [A-z]
}

build() {
  cd "${srcdir}/${_pkgname}"
  autoreconf -vfi
  ./configure --prefix=/usr --datarootdir="$pkgdir/usr/share"
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
