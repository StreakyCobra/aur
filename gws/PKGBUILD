# Maintainer: Fabien Dubosson <fabien.dubosson@gmail.com>
pkgname=gws
pkgver=0.1.3
pkgrel=2
pkgdesc="An helper to manage workspaces which contains git repositories"
arch=('any')
url="https://github.com/StreakyCobra/gws"
license=('MIT')
depends=('bash>4.0' 'git')
changelog="ChangeLog"
source=("https://github.com/StreakyCobra/${pkgname}/archive/${pkgver}.tar.gz"
        "gws.patch")
md5sums=('764a76e22a625bd794659df87d67d788'
         '5bdea34cc8a6317588fb09d43dd81848')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"

    patch -p3 < "${srcdir}/gws.patch"
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -D -m755 "gws" "${pkgdir}/usr/bin/gws"
    install -D -m755 "completions/zsh" "${pkgdir}/usr/share/zsh/site-functions/_gws"
    install -D -m755 "completions/bash" "${pkgdir}/usr/share/bash-completion/completions/gws"
}
