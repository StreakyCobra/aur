# Maintainer: Fabien Dubosson <fabien.dubosson@gmail.com>
# Contributor: Greg Maslov <maslov@cs.unc.edu>

pkgbase="wfdb"
pkgname=('wfdb' 'wfdb-samples')
pkgver="10.5.23"
pkgrel="1"
pkgdesc="Software from PhysioNet for viewing, analyzing, and creating recordings of physiologic signals"
url="http://www.physionet.org/physiotools/wfdb.shtml"
license=('GPL')
arch=('i686' 'x86_64')
depends=('curl>=7.10' 'expat')
optdepends=('wfdb-samples')
makedepends=('gcc' 'chrpath')
install="${pkgname}.install"
changelog="ChangeLog"
source=("http://www.physionet.org/physiotools/${pkgname}.tar.gz"
        "correct-makefiles.patch")
md5sums=('0c4dd0bc86408709fe2364f6703c5c66'
         '08559a75a18b89a78cd038f833e57e7b')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Fixing these Makefiles to install to a different directory from the one being compiled for was... an adventure.
    patch -p3 -i "$srcdir/correct-makefiles.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr

    # From previous PKGBUILD (don't know why it is required)
    ln -s lib wfdb
    (cd lib && ln -s libwfdb.so.10.5 libwfdb.so)

    make
}

package_wfdb() {
    cd "${srcdir}/${pkgbase}-${pkgver}"

    # Weird Makfile that compile stuff at install time, so need to run it here
    make WFDBROOT="${pkgdir}/usr" install

    # Adapt some compile-time hardcoded paths
    sed -i "s+${pkgdir}++g" "${pkgdir}/usr/bin/cshsetwfdb" "${pkgdir}/usr/bin/setwfdb" "${pkgdir}/usr/bin/pnwlogin"

    # Remove RPATH and RUNPATH from executables
    for ex in `find "${pkgdir}/usr/bin/" -exec file {} \; | grep ELF | cut -d':' -f1`;
    do
        chrpath -d "$ex"
    done

    # Delete samples (split package)
    rm "${pkgdir}/usr/database" -rf
}

package_wfdb-samples() {
    arch=('any')
    unset depends
    unset optdepends
    unset install

    cd "${srcdir}/${pkgbase}-${pkgver}"

    # Weird Makfile that compile stuff at install time, so need to run it here
    make WFDBROOT="${pkgdir}/usr" install

    # Delete all except samples
    rm "${pkgdir}/usr/bin" -rf
    rm "${pkgdir}/usr/include" -rf
    rm "${pkgdir}/usr/lib" -rf
    rm "${pkgdir}/usr/share" -rf
}

# vim:set ts=4 sw=4 et:
